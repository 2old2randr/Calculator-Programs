@ Convert date on stack to Julian day number
\<< D2YMD \-> y m d
    \<<
        y m 100. / + d 10000. / +    @ Date as YYYY.MMDDdd
        IF m 2 \<=
        THEN y 1 - m 12 +
        ELSE y m
        END
        \-> dat y1 m1
        \<<
            @ If date is in Gregorian calendar, adjust y1
            IF dat 1582.1015 \>=
            THEN y1 100. / IP DUP 4. / IP SWAP - 2. +
            ELSE 0.
            END                   @ Constant B
            365.25 y1 *
            IF y1 0. <
            THEN .75 -
            END
            IP                    @ Constant C
            30.6001 m1 1. + * IP  @ Constant D
            + + d + 1720994.5 +   @ JD=B+C+D+d+1720994.5
        \>>
    \>>
\>>
